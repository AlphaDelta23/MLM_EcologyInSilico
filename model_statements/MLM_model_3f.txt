model {
  # Priors
  psi.mean ~ dbeta(1,1)
  p.detect.mean ~ dbeta(1,1)

  sd.psi ~ dunif(0,10)
  psi.tau <- pow(sd.psi, -2)
  
  sd.p.detect ~ dunif(0,10)
  p.detect.tau <- pow(sd.p.detect, -2)

  for(i in 1:Nspecies){
    alpha[i] ~ dnorm(logit(psi.mean), psi.tau)T(-12,12)
    lp.detect[i] ~ dnorm(logit(p.detect.mean), p.detect.tau)T(-12,12)
    p.detect[i] <- exp(lp.detect[i]) / (1 + exp(lp.detect[i]))
  }
  
  mean.beta1 ~ dnorm(0, 0.01)
  sd.beta1 ~ dunif(0, 10)
  tau.beta1 <- pow(sd.beta1, -2)
  for(i in 1:Nspecies){
    betas[i, 1] ~ dnorm(mean.beta1, tau.beta1)
  }
  
  mean.beta2 ~ dnorm(0, 0.01)
  sd.beta2 ~ dunif(0, 10)
  tau.beta2 <- pow(sd.beta2, -2)
  for(i in 1:Nspecies){
    betas[i, 2] ~ dnorm(mean.beta2, tau.beta2)
  }
  
  beta_f ~ dnorm(0, 0.01)
  for (i in 1:Nspecies){
    betas[i, 3] <- beta_f
  }

  # Likelihood
  for(i in 1:Nobs){
    logit(psi[i]) <- alpha[Species[i]] + inprod(betas[Species[i],], X[i, ])
    z[i] ~ dbern(psi[i])
    Y[i] ~ dbinom(z[i] * p.detect[Species[i]], J[i])
  }
}
